buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.github.johnrengelman:shadow:8.1.1'
    }
}

apply plugin: 'java'
apply plugin: 'java-library'

group = project.maven_group
version = project.plugin_version

allprojects {
    repositories {
        mavenCentral();
        maven { url = "https://repo.maven.apache.org/maven2/" }
        maven { url = "https://repo.papermc.io/repository/maven-public/" }
        maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
        maven { url = "https://oss.sonatype.org/content/groups/public/" }
        maven { url = "https://papermc.io/repo/repository/maven-public/" }
        maven { url = "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        compileOnly "io.lettuce:lettuce-core:${project.lettuce_version}"
        compileOnly "mysql:mysql-connector-java:${project.mysql_version}"
        compileOnly "cloud.commandframework:cloud-paper:${project.cloudpaper_version}"
        compileOnly "com.google.guava:guava:${project.guava_version}"
    }
}

configurations {
    modules
    compile.extendsFrom(modules)
}

dependencies{
    modules project(':toolkit')
    modules project(':core')
    modules project(':paper')
    modules project(':velocity')
}

def projects = [":toolkit", ":core", ":paper", ":velocity"]
jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn(projects.collect {it + ":shadowJar"} )

    from configurations.modules.collect {
        it.isDirectory() ? it : zipTree(it)
    }

    from(files("LICENSE_rusty-connector"))

    exclude("META-INF/*.RSA", "META-INF/*.DSA", "META-INF/*.SF")
}