compileJava {
    options.encoding = "UTF-8"
}

dependencies {
    // Include in jar
    implementation "club.minnced:discord-webhooks:${project.discord_version}"
    implementation "com.sparkjava:spark-core:${project.spark_version}"
    implementation "net.kyori:adventure-text-minimessage:${project.minimessage_version}"
    implementation "one.microstream:microstream-storage-embedded:${project.microstream_version}"
    implementation "one.microstream:microstream-afs-sql:${project.microstream_sql_version}"
    implementation "org.mariadb.jdbc:mariadb-java-client:${project.mariadb_version}"
    implementation "io.kubernetes:client-java:${project.kubernetes_version}"
    implementation "de.gesundkrank.jskills:jskills:${project.jskills_version}"

    // Dev Only
    shadow project(path: ':core', configuration: 'shadow')
}

// If ever strange behaviour check the docs for reference: https://imperceptiblethoughts.com/shadow/
shadowJar {
    archiveBaseName.set('velocity')
    archiveClassifier.set('')
    archiveVersion.set('')

    mergeServiceFiles()
    minimize()
}

jar.finalizedBy('shadowJar')

def plugin_version = project.plugin_version
processResources {
    def props = ['version': plugin_version, 'config_version': velocity_config_version]
    inputs.properties props
    filteringCharset "UTF-8"

    filesMatching("velocity-plugin.json") {
        expand props
    }
}