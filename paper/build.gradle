compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

dependencies {
    implementation 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'
    implementation 'org.incendo:cloud-paper:2.0.0-beta.10'

    implementation project(path: ':common')
    implementation project(path: ':serverCommon', 'configuration': 'shadow')
}

// If ever strange behaviour check the docs for reference: https://imperceptiblethoughts.com/shadow/
shadowJar {
    archiveBaseName.set('rustyconnector-paper')
    mergeServiceFiles()
    minimize()
    from(processResources)
}

jar.finalizedBy('shadowJar')

def targetJavaVersion = project.java_version as int
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = ['version': plugin_version, 'config_version': config_version, 'magic_link_protocol_version': magic_link_protocol_version]
    inputs.properties props
    filteringCharset 'UTF-8'

    filesMatching('plugin.yml') {
        expand props
    }
}