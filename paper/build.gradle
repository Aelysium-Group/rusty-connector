def plugin_version = project.plugin_version

compileJava {
    options.encoding = "UTF-8"
}

dependencies {
    implementation files("../libs/rustyconnector-core-0.9.0.jar")

    compileOnly "io.papermc.paper:paper-api:${project.paper_version}"
    compileOnly "dev.folia:folia-api:${project.folia_version}"
    implementation "org.incendo:cloud-paper:${project.cloud_version}"
    implementation "org.incendo:cloud-annotations:${project.cloud_annotations_version}"
}

// If ever strange behaviour check the docs for reference: https://imperceptiblethoughts.com/shadow/
shadowJar {
    archiveBaseName.set('paper')
    archiveClassifier.set('')
    archiveVersion.set('')

    mergeServiceFiles()
    minimize()
}

jar.finalizedBy('shadowJar')

def targetJavaVersion = project.java_version as int
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = ['version': plugin_version, 'config_version': config_version, 'magic_link_protocol_version': magic_link_protocol_version]
    inputs.properties props
    filteringCharset 'UTF-8'

    filesMatching('plugin.yml') {
        expand props
    }
}