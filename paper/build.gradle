compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs << '-parameters'
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT"
    implementation "org.incendo:cloud-paper:2.0.0-beta.10"

    implementation project(path: ':serverCommon', configuration: 'shadow')
    implementation project(path: ':common', configuration: 'shadow')
}

processResources {
    def props = ['version': plugin_version, 'config_version': config_version, 'magic_link_protocol_version': magic_link_protocol_version]
    inputs.properties props
    filteringCharset 'UTF-8'

    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveBaseName.set("rustyconnector-paper")
    archiveClassifier.set('')
    mergeServiceFiles()

    relocate "org.incendo", "group.aelysium.rustyconnector.paper.org.incendo"
}

jar.finalizedBy(shadowJar)