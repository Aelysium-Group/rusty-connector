compileJava {
    options.encoding = "UTF-8"
}

dependencies {
    implementation files("../libs/rustyconnector-core-0.9.0.jar")

    implementation 'org.bstats:bstats-velocity:3.0.2'
    implementation "com.velocitypowered:velocity-api:${project.velocity_version}"
    implementation "org.incendo:cloud-velocity:${project.cloud_version}"
    implementation "org.incendo:cloud-annotations:${project.cloud_annotations_version}"
}

// If ever strange behaviour check the docs for reference: https://imperceptiblethoughts.com/shadow/
shadowJar {
    archiveBaseName.set('velocity')
    archiveClassifier.set('')
    archiveVersion.set('')

    relocate 'org.bstats', 'group.aelysium.rustyconnector'

    mergeServiceFiles()
    minimize()
}

jar.finalizedBy('shadowJar')

def plugin_version = project.plugin_version
processResources {
    def props = ['version': plugin_version, 'config_version': config_version, 'magic_link_protocol_version': magic_link_protocol_version]
    inputs.properties props
    filteringCharset "UTF-8"

    filesMatching("velocity-plugin.json") {
        expand props
    }
}