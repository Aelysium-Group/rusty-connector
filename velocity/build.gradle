compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs << '-parameters'
}

dependencies {
    implementation 'org.bstats:bstats-velocity:3.1.0'
    compileOnly "com.velocitypowered:velocity-api:3.3.0-SNAPSHOT"
    implementation "org.incendo:cloud-velocity:2.0.0-beta.10"

    implementation project(path: ':common', configuration: 'shadow')
}

// If ever strange behaviour check the docs for reference: https://imperceptiblethoughts.com/shadow/
shadowJar {
    archiveBaseName.set('rustyconnector-velocity')
    relocate 'org.bstats', 'group.aelysium.rustyconnector'
    mergeServiceFiles()
    minimize()
}

jar.finalizedBy('shadowJar')

def plugin_version = project.plugin_version
processResources {
    def props = ['version': plugin_version, 'config_version': config_version, 'magic_link_protocol_version': magic_link_protocol_version]
    inputs.properties props
    filteringCharset "UTF-8"

    filesMatching("velocity-plugin.json") {
        expand props
    }
}